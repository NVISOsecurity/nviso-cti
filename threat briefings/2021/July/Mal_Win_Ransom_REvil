import "pe"

rule Mal_Win_Ransom_REvil
{
            meta:
            description = "REvil sample April 2021"
            author = "Blackberry Threat Research"
            date = "2021-04"

            strings:
            $s1 = "bootcfg /raw /a /safeboot:network /id 1" nocase ascii
            $s2 = "bcdedit /set" nocase ascii
            $s3 = "safeboot network" nocase ascii
            $s4 = "Domain" nocase wide
            $s5 = "StopService" nocase wide
            $s6 = "GetOwner" nocase wide
            $s7 = "ERR0R D0UBLE RUN!" nocase wide
            $s8 = "k-Err-" nocase wide
            $s9 = "Win32_Service" nocase wide

            condition:
            //PE File
            uint16(0) == 0x5A4D and
            // Filesize
            filesize < 130KB and
            // Import Hash
            pe.imphash() == "031931d2f2d921a9d906454d42f21be0" and
            // Five PE Sections
            pe.number_of_sections == 5 and
            // All Strings
            all of them
}
